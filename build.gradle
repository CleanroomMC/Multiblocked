buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
        maven {url='https://repo.spongepowered.org/maven/'}
        maven {
            url = 'https://maven.cleanroommc.com'
        }
        maven {
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        if (project.use_mixins) {
            classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        }
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

if (project.use_mixins) {
    apply plugin: 'org.spongepowered.mixin'
}

version = project.mod_version
group = project.maven_group
archivesBaseName = project.archives_base_name

sourceCompatibility = targetCompatibility = '1.8'

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = '1.12.2-14.23.5.2847'
    runDir = 'run'
    mappings = 'stable_39'
    def args = []
    if (project.use_coremod) {
        args << '-Dfml.coreMods.load=' + coremod_plugin_class_name
    }
    if (project.use_mixins) {
        args << '-Dmixin.hotSwap=true'
        args << '-Dmixin.checks.interfaces=true'
        args << '-Dmixin.debug.export=true'
    }
    clientJvmArgs.addAll(args)
    serverJvmArgs.addAll(args)
}

repositories {
    maven {
        url = 'https://maven.cleanroommc.com'
    }
    maven { // CraftTweaker
        url = "https://maven.blamejared.com/"
    }
    maven {
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
    }
}

dependencies {

    if (project.use_mixins) {
        compile 'zone.rong:mixinbooter:4.2'
    }
    compile ('software.bernie.geckolib:forge-1.12.2-geckolib:3.0.21')
    compile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.673"
    compile "mezz.jei:jei_1.12.2:4.15.0.293"
    compile files("libs/Baubles-1.12-1.5.2.jar")
    compile files("libs/Botania+r1.10-364.4.jar")
    compile files("libs/MCMultiPart-2.5.3.jar")
    compile files("libs/Mekanism-1.12.2-9.8.3.390.jar")
    compile files("libs/Thaumcraft-1.12.2-6.1.BETA26.jar")
    compile files("libs/NuclearCraft-2o.5.5-1.12.2.jar")
    compile files("libs/QMD-1.0.4-1.12.2.jar")
    compile files("libs/ThaumicJEI-1.12.2-1.6.0-27.jar")
    compile files("libs/gregtech-1.12.2-2.1.4-beta.jar")
    compile files("CodeChickenLib-1.12.2-3.2.3.358-universal.jar")
}

sourceSets {

    main {
        ext.refMap = 'mixins.' + archives_base_name + '.refmap.json'
    }

}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    rename '(.+_at.cfg)', 'META-INF/$1' // Access Transformers
}

jar {
    manifest {
        def attribute_map = [:]
        if (project.use_coremod) {
            attribute_map['FMLCorePlugin'] = project.coremod_plugin_class_name
        }
        if (project.use_mixins) {
            attribute_map['TweakClass'] = 'org.spongepowered.asm.launch.MixinTweaker'
        }
        attribute_map['FMLAT'] = 'multiblocked_at.cfg'
        attribute_map['ForceLoadAsMod'] = true
        attributes(attribute_map)
    }
}